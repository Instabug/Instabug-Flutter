group 'com.instabug.flutter'
version '16.0.1'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
}

apply plugin: 'com.android.library'

android {
    if (project.android.hasProperty("namespace")) {
        namespace "com.instabug.flutter"
    }
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.txt'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    api 'com.instabug.library:instabug:16.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-inline:3.12.1"
    testImplementation "io.mockk:mockk:1.13.13"
    testImplementation "org.robolectric:robolectric:4.12.2"
}

// add upload_symbols task
apply from: './upload_symbols.gradle'
tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.finalizedBy upload_symbols_task
    }
}
