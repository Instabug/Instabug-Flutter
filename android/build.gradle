group 'com.instabug.instabugflutter'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.4'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.txt'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}
dependencies {
    implementation 'com.instabug.library:instabug:10.11.1'
    testImplementation 'junit:junit:4.12'
}
/**
 * get upload enable flag
 * @return true if upload is enabled
 */
Boolean getUploadFlag() {
    try {
        final kFlag = 'instabugUploadEnable'
        final flag = get_variable(kFlag)
        println "$kFlag: $flag"
        return flag?.toBoolean()
    } catch (Exception e) {
        println "Instabug err: uploading symbols: ${e.message}"
    }
}
// add upload_symbols task
apply from: './upload_symbols.gradle'
tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        loadLocalProperties()
        if (getUploadFlag())
            task.finalizedBy upload_symbols_task
    }
}
